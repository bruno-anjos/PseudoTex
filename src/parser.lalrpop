use crate::my_parser::{Expr, Special};

grammar;

pub Main: Expr = {
    <s: State> <b: MainBody> => Expr::MultipleExpr{ e1: Box::new(s), e2: Box::new(b) },
    <i: Interface> <b: MainBody> => Expr::MultipleExpr{ e1: Box::new(i), e2: Box::new(b) },
    <i: Interface> <s: State> <b: MainBody> =>
        Expr::MultipleExpr{
            e1: Box::new(
                Expr::MultipleExpr{
                    e1: Box::new(i),
                    e2: Box::new(s)
                }
            ),
            e2: Box::new(b)
        },
    <b: MainBody> => b,
};

pub Interface: Expr = {
    INTERFACE <r: Requests> <i: Indications> END => Expr::Interface{ reqs: Box::new(r), indics: Box::new(i) },
    INTERFACE <r: Requests> END => Expr::Interface{ reqs: Box::new(r), indics: Box::new(Expr::Empty) },
    INTERFACE <i: Indications> END => Expr::Interface{ reqs: Box::new(Expr::Empty), indics: Box::new(i) },
};

pub Requests: Expr = {
    R <r: MultiCall> => Expr::Requests{ requests: Box::new(r) }
};

pub Indications: Expr = {
    I <i: MultiCall> => Expr::Indications{ indications: Box::new(i) }
};

pub MultiCall: Expr = {
    <m: MethodCall> => m,
    <m: MethodCall> <ms: MultiCall> => Expr::MultipleExpr{ e1: Box::new(m), e2: Box::new(ms) },
};

pub MethodCall: Expr = {
    <s: STRING> OPEN_PAR <args: Args> CLOSE_PAR => Expr::MethodCall{ name: s.to_string(), args: args }
};

pub State: Expr = {
    STATE <e: MultiLineMultiTerm> END => Expr::State{ body: Box::new(e) }
};

pub MainBody: Expr = {
    BEGIN <e:Any> END => e
};

pub Any: Expr = {
    <b: BasicExpr> => b,
    <b: BasicExpr> <e: Any> => Expr::MultipleExpr{ e1: Box::new(b), e2: Box::new(e) }
};

pub BasicExpr: Expr = {
    <e: Method> => e,
    <e: Assign> => e,
    <e: If> => e,
    <e: Trigger> => e,
};

pub Method: Expr = {
    SimpleMethod,
    ComplexMethod,
    SimpleEvent,
    ComplexEvent
};

pub Trigger: Expr = {
    TRIGGER <m: MethodCall> => Expr::Trigger{ method: Box::new(m) },
}

pub SimpleMethod: Expr = {
    UPON <id: STRING> DO COLON <e: Any> END => Expr::Method{ name: id.to_string(), body: Box::new(e) }
};

pub ComplexMethod: Expr = {
    UPON <id: STRING> OPEN_PAR <args: Args> CLOSE_PAR DO COLON <e: Any> END =>
        Expr::MethodWithArgs{ name: id.to_string(), args: args, body: Box::new(e) }
};

pub SimpleEvent: Expr = {
    UPON EVENT <id: STRING> DO COLON <e: Any> END => Expr::Event{ name: id.to_string(), body: Box::new(e) }
};

pub ComplexEvent: Expr = {
    UPON EVENT <id: STRING> OPEN_PAR <args: Args> CLOSE_PAR DO COLON <e: Any> END =>
        Expr::EventWithArgs{ name: id.to_string(), args: args, body: Box::new(e) }
};

pub Args: Vec<String> = {
    <a: STRING> => vec![a.to_string()],
    <a: STRING> COMMA <mut args: Args> => { let mut res = vec![a.to_string()]; res.append(&mut args); res }
};

pub Assign: Expr = {
    <id:STRING> EQ <value:STRING> => Expr::Assign{ name: id.to_string(), value: Box::new(Expr::String(value.to_string())) },
    <id:STRING> EQ <value:MethodCall> => Expr::Assign{ name: id.to_string(), value: Box::new(value) }
};

pub If: Expr = {
    IF <condition: SingleLineMultiTerm> THEN <body: Any> END =>
        Expr::If{ condition: Box::new(condition), body: Box::new(body) },
    IF <condition: SingleLineMultiTerm> THEN <if_body: Any> ELSE <else_body: Any> END =>
        Expr::IfElse{ condition: Box::new(condition), if_body: Box::new(if_body), else_body: Box::new(else_body) },
};

pub Special: Special = {
    IN => Special::In,
    EXISTS => Special::Exists,
};

pub MultiLineMultiTerm: Expr = {
    <e: Term> => e,
    <e1: Term> <e2: MultiLineMultiTerm> => Expr::MultiLineMultipleTerm { t1: Box::new(e1), t2: Box::new(e2) }
};

pub SingleLineMultiTerm: Expr = {
    <e: Term> => e,
    <e1: Term> <e2: SingleLineMultiTerm> => Expr::SingleLineMultipleTerm { t1: Box::new(e1), t2: Box::new(e2) }
};

pub Term: Expr = {
    <s: STRING> => Expr::String(s.to_string()),
    <s: Special> => Expr::Special(s),
};

match {
    "="     => EQ,
    "upon"  => UPON,
    "event" => EVENT,
    "do"    => DO,
    ":"     => COLON,
    ","     => COMMA,
    "("     => OPEN_PAR,
    ")"     => CLOSE_PAR,
    "state" => STATE,
    "interface" => INTERFACE,
    "r"     => R,
    "i"     => I,
    "trigger" => TRIGGER,
    "begin" => BEGIN,
    "end"   => END,
    "in"    => IN,
    "if"    => IF,
    "else"  => ELSE,
    "then"  => THEN,
    "exists"=> EXISTS,
    r"\w+"  => STRING
}
